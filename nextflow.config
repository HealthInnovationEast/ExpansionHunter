// Please do not change the order of the numbered sections!
// The expected order is: 1. Parameters - 2. Profiles - 3. Process -  4. Executor
// There is a high chance it would break the configuration of 'profiles'

manifest {
    name            = 'cynapse-ccri/ExpansionHunter'
    homePage        = 'https://github.com/cynapse-ccri/ExpansionHunter'
    description     = 'Nextflow implementation of Illumina/ExpansionHunter (multi-sample)'
    mainScript      = 'main.nf'
    version         = '0.3.0'
}

// 1. Parameters

// NOTE:
// Initialise the values of the params to the preferred default value or to false
params {
    // output folder options
    outdir          = 'results'
    tracedir        = "${params.outdir}/pipeline_info"

    // client-specific variables to be used in pipeline introspection reporting
    raci_owner      = "NA"
    domain_keywords = "NA"

    // required inputs
    sample_info             = null
    reference               = null
    variant_catalog         = null
    // optional - set to defaults (from original code base)
    region_extension_length = 1000
    aligner                 = 'dag-aligner'
    analysis_mode           = 'seeking'
    augment                 = false
    variants                 = 'NO_FILE'
    multistr                = 'NO_FILE'

    // report_dir is:
    // - the folder from the container that includes the scripts for NF <= v20.01 (bin)
    // - the ${projectDir}/bin folder of the root of the repo with the scripts for NF >= v20.10
    report_dir = '/opt/bin/'

    // when set to true, prints help and exits
    help = false

    // default container for all processes, excluding those defined differently via the usage with 'withName'
    container = 'quay.io/lifebitai/ubuntu:18.10'

    // process resources defaults
    cpus   = 1
    memory = 4.GB
    disk   = 100.GB

    // max resources limits defaults
    max_cpus   = 16
    max_memory = 192.GB
    max_time   = 24.h

    // execution related defaults
    config        = 'conf/standard.config'
    echo          = false
    errorStrategy = { task.exitStatus in [143,137,104,134,139] ? 'retry' : 'terminate' }
    maxRetries    = 9
    maxForks      = 200
    queueSize     = 200
    executor      = false

    // google-lifesciences
    gls_bootDiskSize = '50.GB'
    gls_preemptible  = true
    zone             = 'us-east1-b'
    network          = 'default'
    subnetwork       = 'default'
}

// 2. Profiles


// Do not update the order because the values set in params scope will not be overwritten
// Do not attempt to simplify to
// includeConfig params.config
// outside of profiles scope, it will fail to update the values of the params
profiles {
    standard {includeConfig params.config}
    docker { docker.enabled = true }
    base {includeConfig 'conf/base.config'}
    google {includeConfig 'conf/google.config'}
    test {includeConfig 'conf/test.config'} // must be before singularity
    local {includeConfig 'conf/test.config'}
    singularity {includeConfig 'conf/singularity.config'}
    slurm {includeConfig 'conf/slurm.config'}
    awsbatch {includeConfig 'conf/awsbatch.config'}
    cynapse_pro_admin {includeConfig 'conf/cynapse-pro-admin.config'}
    cynapse_pro_wrkspc {includeConfig 'conf/cynapse-pro-wrkspc.config'}
}

// 3. Process

// Do not change order of block, must follow after profiles scope (last section that updates params)
process {
    echo          = params.echo
    cpus          = params.cpus
    memory        = params.memory
    maxRetries    = params.maxRetries
    maxForks      = params.maxForks
    container     = params.container
    errorStrategy = params.errorStrategy

    withName: obtain_pipeline_metadata {
        cpus = 1
        memory = 500.MB
    }

    withName: expansion_hunter {
        container = 'quay.io/wtsicgp/expansion_hunter:5.0.0'
        // cpus   = { workflow.stubRun ? 1 : resource_max(params.cpus) }
        // memory = { workflow.stubRun ? 1.GB : resource_max(task.cpus * params.memory * task.attempt) }
        cpus   = { resource_max(params.cpus) }
        memory = { resource_max(task.cpus * params.memory * task.attempt) }
    }

    withName: sort_n_index {
        container = 'quay.io/wtsicgp/expansion_hunter:5.0.0'
        // cpus   = { workflow.stubRun ? 1 : resource_max(params.cpus) }
        // memory = { workflow.stubRun ? 1.GB : resource_max(task.cpus * params.memory * task.attempt) }
        cpus   = { resource_max(params.cpus, 4) } // caps at 4cpus
        memory = { resource_max(task.cpus * params.memory * task.attempt) }
    }

    withName: augment {
        container = 'quay.io/cynapse-ccri/expansionhunter:0.4.2'
        // cpus   = { workflow.stubRun ? 1 : resource_max(params.cpus) }
        // memory = { workflow.stubRun ? 1.GB : resource_max(task.cpus * params.memory * task.attempt) }
        cpus   = { resource_max(params.cpus, 4) } // caps at 4cpus
        memory = { resource_max(task.cpus * params.memory * task.attempt) }
    }
}

// 4. Executor - Do not remove this section! Required for running with different executors using --executor parameter

executor {
    name      = params.executor
    queueSize = params.queueSize
}

// 5. Nextflow timeline, report and trace reports

def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
    enabled = true
    file    = "${params.tracedir}/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "${params.tracedir}/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    fields  = 'task_id,native_id,process,name,tag,status,exit,cpus,%cpu,memory,%mem,rss,vmem,peak_rss,peak_rss,container'
    file    = "${params.tracedir}/execution_trace_${trace_timestamp}.txt"
}

def resource_max(obj_in) {
    /**
     * See resource_max(obj_in, alt_max)
     */
    return resource_max(obj_in, null)
}

def resource_max(obj_in, alt_max) {
    /**
     * Check resource isn't over max, optionally override the global max
     *
     * @param obj nextflow.util.MemoryUnit, nextflow.util.Duration or int
     * @param obj null or value lower than global max for matching data type as first element
     * @return obj Lower value of input and appropriate params.max_* (or alt_max)
     *
     * Only use for CPU, Memory or Duration (time)
     * Expects params for the following to exist:
     * - params.max_memory
     * - params.max_time
     * - params.max_cpus
     */
    if (obj_in instanceof nextflow.util.MemoryUnit) {
        max = alt_max == null ? params.max_memory : alt_max
        if (nextflow.util.MemoryUnit.compareTo(max, obj_in) < 0) {
            return max as nextflow.util.MemoryUnit
        }
        return obj_in
    }
    if (obj_in instanceof nextflow.util.Duration) {
        max = alt_max == null ? params.max_time : alt_max
        if (nextflow.util.Duration.compareTo(max, obj_in) < 0) {
            return max as nextflow.util.Duration
        }
        return obj_in
    }
    max = alt_max == null ? params.max_cpus : alt_max
    if (max < obj_in) {
        return max as int
    }
    return obj_in
}
